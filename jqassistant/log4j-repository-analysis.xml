<jqassistant-rules xmlns="http://schema.jqassistant.org/rule/v1.11" >

    <group id="log4j-repository-analysis">
        <includeConcept refId="log4j-repository-analysis:*"/>
    </group>

    <concept id="log4j-repository-analysis:ArtifactDependencies">
        <description>Propagates dependencies declared by POM descriptors to artifacts.</description>
        <cypher><![CDATA[
            call apoc.periodic.iterate(
              "MATCH
                 (pom:Effective:Pom),
                 (pom)-[:DESCRIBES]->(artifact:Artifact),
                 (pom)-[:DECLARES_DEPENDENCY]->()-[:TO_ARTIFACT]->(dependency:Artifact)
               WHERE
                 artifact.type in $artifactTypes
               RETURN
                 artifact, dependency",
              "MERGE (artifact)-[:DEPENDS_ON]->(dependency)",
              {
                params: {
                  artifactTypes: ["jar","war","ear"]
                }
              })
            YIELD
              total as `Artifact Dependencies`
        ]]></cypher>
        <verify>
            <aggregation />
        </verify>
    </concept>

    <concept id="log4j-repository-analysis:DirectLog4jDependencies">
        <description>Reports all POM descriptors declaring a direct dependency to any Log4j 2.x artifact.</description>
        <cypher><![CDATA[
            MATCH
              (:Repository)-[:CONTAINS_POM]->(pom:Pom),
              (pom)-[:DECLARES_DEPENDENCY]->()-[:TO_ARTIFACT]->(log4j:Artifact)
            WHERE
              log4j.fqn STARTS WITH "org.apache.logging.log4j"
            RETURN
              pom.fqn as POM, collect(log4j.fqn) as `Declared Log4j dependencies`
            ]]></cypher>
        <report type="csv" />
    </concept>

    <concept id="log4j-repository-analysis:DeployableArtifact">
        <description>Label artifacts of type WAR, EAR, Spring Boot fat JARs and JARs with a main class declaration as "Deployable".</description>
        <cypher><![CDATA[
            MATCH
              (:Repository)-[:CONTAINS_ARTIFACT]->(deployable:Artifact)
            WHERE
              (
                // WAR and EAR files
                deployable.type in ["war","ear"]
                // Spring Boot fat JARs
                or (deployable)<-[:DESCRIBES]-(:Pom)-[:USES_PLUGIN]->(:Plugin)-[:IS_ARTIFACT]->(:Artifact{name:"spring-boot-maven-plugin"})
                // Any JAR containing a main class declaration
                or (deployable)<-[:DESCRIBES]-(:Pom)-[:USES_PLUGIN]->(:Plugin)-[:HAS_EXECUTION|:HAS_CONFIGURATION*]->(:Configuration)-[:CONTAINS]->(:Value{name:"archive"})-[:CONTAINS]->(:Value{name:"manifest"})-[:CONTAINS]->(:Value{name:"mainClass"})
              )
              and not exists(deployable.classifier)
            SET
              deployable:Deployable
            RETURN
              deployable.fqn as Deployable
            ]]></cypher>
    </concept>

    <concept id="log4j-repository-analysis:TransitiveLog4jDependencies">
        <requiresConcept refId="log4j-repository-analysis:DeployableArtifact"/>
        <requiresConcept refId="log4j-repository-analysis:ArtifactDependencies"/>
        <description>Reports deployable artifacts having direct or indirect dependencies to Log4j core 2.x.</description>
        <cypher><![CDATA[
            MATCH
              (deployable:Deployable)
// Uncomment if you want to restrict to deployable artifacts younger than a given timestamp
//          WHERE
//            deployable.lastModified > datetime('2021-01-01T00:00:00Z').epochMillis
            WITH
              deployable
            MATCH
              p = shortestPath((deployable)-[:DEPENDS_ON*]->(log4j:Artifact))
            WHERE
              deployable <> log4j
              and log4j.fqn STARTS WITH 'org.apache.logging.log4j:log4j-core:'
            RETURN
              deployable.fqn as Deployable,
              case exists(deployable.lastModified) when true then datetime({epochMillis:deployable.lastModified}) else "" end as `Last Modified`,
              collect(DISTINCT log4j.version) as `Log4j dependencies`
            ORDER BY
              `Last Modified`
            ]]></cypher>
        <report type="csv" />
    </concept>

</jqassistant-rules>
